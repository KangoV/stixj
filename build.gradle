plugins {
    id 'idea'
    id 'maven-publish'
    id 'io.micronaut.library' version '4.2.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id 'com.adarshr.test-logger' version '2.0.0'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

micronaut {
    version("4.2.1")
}

scmVersion {
    versionCreator System.getenv('USE_BRANCH_VERSION') != null ? 'versionWithBranch' : 'simple'
    versionIncrementer 'incrementMinor' // default=incrementPatch
    ignoreUncommittedChanges = false
    branchVersionIncrementer = [
            'hotfix/.*'  : { c -> c.currentVersion.incrementPatchVersion() },
            'release/.*' : 'incrementMinorIfNotOnRelease'
    ]
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, c -> /version: $v/}, replacement: {v, c -> "vers/ion: $v"}]
        pre 'commit'
    }
    localOnly = true
}

version = scmVersion.version
group = "io.kangov.stixj"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    annotationProcessor "org.immutables:value:2.10.0"
    annotationProcessor "io.micronaut.validation:micronaut-validation-processor"
    annotationProcessor "io.micronaut:micronaut-inject-java"

    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut.validation:micronaut-validation"

//    implementation("io.micronaut.beanvalidation:micronaut-hibernate-validator")

    implementation platform("com.fasterxml.jackson:jackson-bom:2.15.2")
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names"

    implementation "org.immutables:value:2.10.0"
    implementation "org.immutables:annotate:2.10.0"
    implementation "org.immutables:serial:2.10.0"

//    implementation("com.networknt:json-schema-validator:1.0.76");

    runtimeOnly "ch.qos.logback:logback-classic:1.2.11"
    runtimeOnly "org.yaml:snakeyaml"

//    testImplementation "org.skyscreamer:jsonassert:1.5.1"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation 'net.andreinc:mockneat:0.4.8'
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.9.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.1"

}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

//tasks.named('shadowJar', ShadowJar) {
//    enableRelocation true
//    relocationPrefix "myapp"
//}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    testlogger {
        theme 'mocha'
        showExceptions true
        showStackTraces true
        showFullStackTraces false
        showCauses true
        slowThreshold 2000
        showSummary true
        showSimpleNames false
        showPassed true
        showSkipped true
        showFailed true
        showStandardStreams false
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
    }
}



